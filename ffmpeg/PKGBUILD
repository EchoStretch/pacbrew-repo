pkgname=ps5-payload-ffmpeg
pkgver=7.0.1
pkgrel=1
pkgdesc='ffmpeg port'
arch=('x86_64')
url='https://ffmpeg.org'
license=('LGPL' 'GPL')
options=(!strip libtool staticlibs)
source=("https://ffmpeg.org/releases/ffmpeg-$pkgver.tar.xz"
	"fftools-ffplay-add-basic-support-for-game-controller-input.diff")
sha256sums=('bce9eeb0f17ef8982390b1f37711a61b4290dc8c2a0c1a37b5857e85bfb0e4ff'
	   '0ad5ccf47158fe56a5d94a87b2307841f129bd68bd2e1b99b5903468dffb83cb')
groups=('ps5-payload-dev')
depends=('ps5-payload-zlib' 'ps5-payload-mbedtls' 'ps5-payload-freetype'
	 'ps5-payload-libfribidi' 'ps5-payload-harfbuzz' 'ps5-payload-libass')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
    cd ffmpeg-$pkgver
    patch -p1 < ../../fftools-ffplay-add-basic-support-for-game-controller-input.diff
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd ffmpeg-$pkgver

    ./configure \
	--prefix="${PREFIX}" --enable-cross-compile \
	--cross-prefix="${PS5_PAYLOAD_SDK}/bin/prospero-" \
	--enable-static --disable-shared \
	--arch=x86_64 --target-os=freebsd \
	--enable-mbedtls --enable-version3 \
	--enable-libfreetype --enable-libfribidi --enable-libharfbuzz \
	--disable-debug --disable-doc \
	--enable-libass \
	--cc="${CC}" --cxx="${CXX}" --nm="${NM}" --strip="${STRIP}" \
	--ar="${AR}" --ranlib="${RANLIB}" --pkg-config="${PKG_CONFIG}"
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd ffmpeg-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
