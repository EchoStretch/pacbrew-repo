pkgname=ps5-payload-sdl2_image
pkgver=2.8.2
pkgrel=1
pkgdesc='SDL2 image loading library.'
arch=('x86_64')
url='https://libsdl.org/projects/SDL_image'
license=('zlib')
options=(!strip libtool staticlibs)
source=("https://github.com/libsdl-org/SDL_image/releases/download/release-${pkgver}/SDL2_image-${pkgver}.tar.gz")
sha256sums=('8f486bbfbcf8464dd58c9e5d93394ab0255ce68b51c5a966a918244820a76ddc')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')
depends=('ps5-payload-sdl2'
	 'ps5-payload-libpng'
	 'ps5-payload-libjpeg-turbo'
	 'ps5-payload-libwebp')

build() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd SDL2_image-$pkgver

    ./configure --prefix="${PS5_SYSROOT}" --host=x86_64 \
		--disable-shared --enable-static \
		--with-sdl-prefix="${PS5_SYSROOT}" \
		--disable-jpg-shared --disable-png-shared --disable-webp-shared
    ${MAKE}
}

package() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd SDL2_image-$pkgver

    ${MAKE} install DESTDIR="$pkgdir"

    echo "Requires.private: libpng libjpeg libwebp" >> "${pkgdir}/${PS5_SYSROOT}/lib/pkgconfig/SDL2_image.pc"
}
