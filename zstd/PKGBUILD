pkgname=ps5-payload-zstd
pkgver=1.5.6
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm'
url='https://facebook.github.io/zstd/'
arch=(x86_64)
license=(BSD-3-Clause GPL-2.0-only)
options=(!strip libtool staticlibs)
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.gz)
sha256sums=('8c29e06cf42aacc1eafc4077ae2ec6c6fcb96a626157e0593d5e82a34fd403c1')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd zstd-${pkgver}

    export prefix="${PS5_HBROOT}"
    export CFLAGS=-DZSTD_NO_TRACE
    ${MAKE} -C lib libzstd.a V=1
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd zstd-${pkgver}

    ${MAKE} -C lib install-pc install-static install-includes \
	    DESTDIR="${pkgdir}/${PS5_SYSROOT}"
}
