pkgname=ps5-payload-libfribidi
pkgver=1.0.15
pkgrel=1
pkgdesc='Free Implementation of the Unicode Bidirectional Algorithm'
arch=('x86_64')
url="https://github.com/fribidi/fribidi"
license=('LGPL2.1')
options=(!strip !debug staticlibs)
source=("https://github.com/fribidi/fribidi/releases/download/v$pkgver/fribidi-$pkgver.tar.xz")
sha256sums=('0bbc7ff633bfa208ae32d7e369cf5a7d20d5d2557a0b067c9aa98bcbf9967587')
makedepends=('ps5-payload-sdk')
groups=('ps5-payload-dev')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd fribidi-$pkgver

    ./configure --prefix="${PS5_HBROOT}" --host=x86_64 \
		--disable-shared --enable-static
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd fribidi-$pkgver

    export DESTDIR="${pkgdir}/${PS5_SYSROOT}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PS5_HBROOT}"
}
