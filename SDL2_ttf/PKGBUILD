pkgname=ps5-payload-sdl2_ttf
pkgver=2.22.0
pkgrel=1
pkgdesc="SDL2 TrueType font library."
arch=('x86_64')
url="https://libsdl.org/projects/SDL_ttf"
license=("zlib")
options=(!strip libtool staticlibs)
source=("${url}/release/SDL2_ttf-${pkgver}.tar.gz")
sha256sums=('d48cbd1ce475b9e178206bf3b72d56b66d84d44f64ac05803328396234d67723')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')
depends=('ps5-payload-sdl2' 'ps5-payload-freetype' 'ps5-payload-harfbuzz')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd SDL2_ttf-$pkgver

    # harfbuzz is currently crashing when used with SDL2_ttf
    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--disable-freetype-builtin \
		--disable-harfbuzz-builtin \
		--disable-harfbuzz
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd SDL2_ttf-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
