pkgname=ps5-payload-expat
pkgver=2.6.2
pkgrel=1
pkgdesc="An XML parser library"
arch=('x86_64')
url="https://libexpat.github.io"
license=('MIT')
source=(https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-$pkgver.tar.bz2)
sha256sums=('9c7c1b5dcbc3c237c500a8fb1493e14d9582146dd9b42aa8d3ffb856a3b927e0')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd expat-$pkgver

    ./configure --prefix="${PS5_HBROOT}" --host=x86_64
    ${MAKE}
}


package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd expat-$pkgver

    export DESTDIR="${pkgdir}/${PS5_SYSROOT}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PS5_HBROOT}"
}
