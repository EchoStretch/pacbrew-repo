pkgname=ps5-payload-sdl2_kitchensink
pkgver=git
pkgrel=1
pkgdesc="FFmpeg and SDL2 based library for audio and video playback, written in C99."
arch=('any')
url="https://katajakasa.github.io/SDL_kitchensink/"
license=('MIT')
source=('git+https://github.com/katajakasa/SDL_kitchensink.git#branch=v2')
sha256sums=('SKIP')

options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')
depends=('ps5-payload-sdl2' 'ps5-payload-ffmpeg' 'ps5-payload-libass')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

pkgver() {
    cd SDL_kitchensink
    printf "2.0.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd SDL_kitchensink

    ${CMAKE} -DCMAKE_BUILD_TYPE=Release \
	     -DBUILD_STATIC=YES \
	     -DBUILD_SHARED=NO \
	     -B build \
             -S .
    ${MAKE} -C build
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd SDL_kitchensink

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} -C build install

    cd $DESTDIR/$PREFIX/lib
    ln -s libSDL_kitchensink_static.a libSDL_kitchensink.a
}
