pkgname=ps5-payload-zlib
pkgver=1.3.1
pkgrel=1
pkgdesc='Deflate compression method library'
arch=('x86_64')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("https://zlib.net/zlib-$pkgver.tar.gz")
sha256sums=('9a93b2b7dfdac77ceba5a558a580e74667dd6fede4585b91eefb60f03b72df23')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd zlib-$pkgver/contrib/minizip
    autoreconf --force --verbose --install
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd zlib-$pkgver

    ./configure --prefix="${PS5_HBROOT}" --static
    ${MAKE}

    # libminizip
    cd contrib/minizip
    ./configure --prefix="${PS5_HBROOT}" --host=x86_64 \
		--enable-static --disable-shared
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd zlib-$pkgver

    export DESTDIR="${pkgdir}/${PS5_SYSROOT}"
    ${MAKE} install

    cd contrib/minizip
    ${MAKE} install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PS5_HBROOT}"
}
