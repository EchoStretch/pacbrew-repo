pkgname=ps5-payload-sdl2_net
pkgver=2.2.0
pkgrel=1
pkgdesc="A small sample cross-platform networking library (Version 2)"
arch=('x86_64')
url="https://github.com/libsdl-org/SDL_net"
license=('MIT')
source=("https://github.com/libsdl-org/SDL_net/releases/download/release-${pkgver}/SDL2_net-${pkgver}.tar.gz")
sha256sums=('4e4a891988316271974ff4e9585ed1ef729a123d22c08bd473129179dc857feb')
depends=('ps5-payload-sdl2')

prepare() {
    cd SDL2_net-$pkgver
    sed -i 's|= gethostbyaddr|= NULL; //|g' SDLnet.c
}

build() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd SDL2_net-$pkgver

    ./configure --prefix="${PS5_SYSROOT}" --host=x86_64 \
		--disable-shared --enable-static
    ${MAKE}
}

package() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd SDL2_net-$pkgver

    ${MAKE} install DESTDIR="$pkgdir"
}
