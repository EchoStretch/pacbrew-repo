pkgname=ps5-payload-flac
pkgver=1.4.3
pkgrel=1
pkgdesc='Free Lossless Audio Codec'
url='https://xiph.org/flac/'
arch=('x86_64')
license=('BSD' 'GPL')
source=(https://github.com/xiph/flac/releases/download/${pkgver}/flac-${pkgver}.tar.xz)
sha256sums=('6c58e69cd22348f441b861092b825e591d0b822e106de6eb0ee4d05d27205b70')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk' 'ps5-payload-libcxx' 'ps5-payload-libogg')

build() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd flac-$pkgver

    ./configure --prefix=${PS5_SYSROOT} --host=x86_64 \
		--enable-static --disable-shared \
		--disable-xmms-plugin --enable-cpplibs \
		--disable-thorough-tests --disable-oggtest

    ${MAKE} -C src/libFLAC
}

package() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd flac-$pkgver

    ${MAKE} -C src/libFLAC DESTDIR="$pkgdir" install
    ${MAKE} -C include/FLAC DESTDIR="$pkgdir" install
    ${MAKE} -C include/FLAC++ DESTDIR="$pkgdir" install
}

