pkgname=ps5-payload-flac
pkgver=1.4.3
pkgrel=1
pkgdesc='Free Lossless Audio Codec'
url='https://xiph.org/flac/'
arch=('x86_64')
license=('BSD' 'GPL')
source=(https://github.com/xiph/flac/releases/download/${pkgver}/flac-${pkgver}.tar.xz)
sha256sums=('6c58e69cd22348f441b861092b825e591d0b822e106de6eb0ee4d05d27205b70')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk' 'ps5-payload-libcxx' 'ps5-payload-libogg')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd flac-$pkgver

    ./configure --prefix="${PS5_HBROOT}" --host=x86_64 \
		--disable-xmms-plugin --enable-cpplibs \
		--disable-thorough-tests --disable-oggtest

    ${MAKE} -C src/libFLAC
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd flac-$pkgver

    export DESTDIR="${pkgdir}/${PS5_SYSROOT}"
    ${MAKE} -C src/libFLAC install
    ${MAKE} -C include/FLAC install
    ${MAKE} -C include/FLAC++ install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PS5_HBROOT}"
}

