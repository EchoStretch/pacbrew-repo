pkgname=ps5-payload-sdl2
pkgver=2.30.2.2126dfa7
pkgrel=1
pkgdesc="A library for portable low-level access to video, audio, joysticks..."
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
depends=('ps5-payload-libsamplerate')
makedepends=('ps5-payload-sdk')
source=("git+https://github.com/ps5-payload-dev/SDL.git#commit=2126dfa7")
sha256sums=('SKIP')
groups=('ps5-payload-dev' 'ps5-payload-sdl2')

build() {
  source /opt/pacbrew/ps5/payload-sdk/shell/toolchain.sh

  cd SDL

  ${CMAKE} -B build \
        -DCMAKE_INSTALL_PREFIX=${PS5_SYSROOT}/usr \
        -DCMAKE_BUILD_TYPE=Release \
        .
  make -C build
}

package() {
  source /opt/pacbrew/ps5/payload-sdk/shell/toolchain.sh

  cd SDL

  make -C build DESTDIR="$pkgdir" install
}

