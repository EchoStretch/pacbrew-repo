pkgname=ps5-payload-libogg
pkgver=1.3.5
pkgrel=1
pkgdesc='Ogg bitstream and framing library'
arch=('x86_64')
url='https://www.xiph.org/ogg'
license=('BSD')
source=("https://ftp.osuosl.org/pub/xiph/releases/ogg/libogg-$pkgver.tar.gz")
sha256sums=('0eb4b4b9420a0f51db142ba3f9c64b333f826532dc0f48c6410ae51f4799b664')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

build() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd libogg-$pkgver

    ${CMAKE} -DCMAKE_INSTALL_PREFIX=${PS5_SYSROOT} \
	     -DCMAKE_BUILD_TYPE=Release \
	     -B build \
	     -S .
    ${MAKE} -C build
}

package() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd libogg-$pkgver

    ${MAKE} -C build install DESTDIR="$pkgdir"
}
