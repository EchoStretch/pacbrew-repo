pkgname=ps5-payload-freetype
pkgver=2.13.2
pkgrel=1
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('x86_64')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.gz")
sha256sums=('1ac27e16c134a7f2ccea177faba19801131116fd682efc1f5737037c5db224b5')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')
depends=("ps5-payload-bzip2" "ps5-payload-libpng" "ps5-payload-zlib")


prepare() {
    export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd freetype-$pkgver

    ./configure --prefix="${PS5_HBROOT}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--with-zlib=yes \
		--with-bzip2=yes \
		--with-png=yes \
		--with-harfbuzz=no \
		--enable-freetype-config
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd freetype-$pkgver

    export DESTDIR="${pkgdir}/${PS5_SYSROOT}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PS5_HBROOT}"

    mkdir -p "${pkgdir}/${PS5_PAYLOAD_SDK}/bin"
    mv "${DESTDIR}/${PS5_HBROOT}/bin/freetype-config" \
       "${pkgdir}/${PS5_PAYLOAD_SDK}/bin/prospero-freetype-config"
}
