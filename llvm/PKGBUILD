pkgname=ps5-payload-llvm
pkgver=14.0.6
pkgrel=1
pkgdesc="LLVM Compiler infrastructure"
arch=('any')
url="https://clang.llvm.org/"
license=('Apache-2.0 WITH LLVM-exception')
options=(!strip libtool staticlibs)
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver/llvm-project-$pkgver.src.tar.xz")
sha256sums=('8b3cfd7bc695bd6cea0f37f53f0981f34f87496e79e2529874fd03a2f9dd3a8a')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd llvm-project-$pkgver.src
    sed -i 's|static_assert(|static_assert(1 or |g' llvm/lib/Support/SmallVector.cpp # TODO

    cmake -S llvm -B host \
          -DCMAKE_BUILD_TYPE=Release \
          -DLLVM_ENABLE_PROJECTS=""

    make -C host llvm-tblgen
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd llvm-project-$pkgver.src

    LLVM_BINDIR=$("${PS5_PAYLOAD_SDK}/bin/llvm-config" --bindir)
    ${CMAKE} -S llvm -B build  \
	     -DCMAKE_BUILD_TYPE=Release \
	     -DLLVM_HOST_TRIPLE=x86_64-sie-ps5 \
	     -DLLVM_TARGETS_TO_BUILD="X86" \
	     -DLLVM_ENABLE_PROJECTS="" \
	     -DLLVM_ENABLE_PLUGINS=NO \
	     -DLLVM_ENABLE_BACKTRACES=NO \
	     -DLLVM_ENABLE_LIBXML2=NO \
	     -DLLVM_BUILD_RUNTIME=NO \
	     -DLLVM_BUILD_TOOLS=NO \
	     -DLLVM_BUILD_LLVM_DYLIB=NO \
	     -DLLVM_INCLUDE_TESTS=NO \
	     -DLLVM_INCLUDE_EXAMPLES=NO \
	     -DLLVM_INCLUDE_BENCHMARKS=NO \
	     -DLLVM_LINK_LLVM_DYLIB=NO \
	     -DLLVM_TOOL_LTO_BUILD=NO \
	     -DLLVM_NATIVE_TOOL_DIR="${LLVM_BINDIR}" \
	     -DLLVM_CONFIG_PATH="${PS5_PAYLOAD_SDK}/bin/llvm-config" \
	     -DLLVM_USE_HOST_TOOLS=TRUE \
	     -DLLVM_TABLEGEN="${PWD}/host/bin/llvm-tblgen"
    ${MAKE} -C build
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd llvm-project-$pkgver.src

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} -C build install
}
