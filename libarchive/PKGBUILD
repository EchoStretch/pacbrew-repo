pkgname=ps5-payload-libarchive
pkgver=3.7.4
pkgrel=1
pkgdesc='library that can create and read several streaming archive format'
arch=('x86_64')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=("http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz")
sha256sums=('7875d49596286055b52439ed42f044bd8ad426aa4cc5aabd96bfe7abb971d5e8')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk' 'ps5-payload-bzip2' 'ps5-payload-zlib'
	     'ps5-payload-zstd' 'ps5-payload-xz' )

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libarchive-$pkgver

    ./configure --prefix="${PS5_HBROOT}" --host=x86_64 \
		--disable-shared --enable-static  \
		--disable-bsdtar --disable-bsdcat \
		--disable-bsdcpio --disable-acl \
		--without-openssl --without-xml2
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libarchive-$pkgver

    export DESTDIR="${pkgdir}/${PS5_SYSROOT}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PS5_HBROOT}"
}

