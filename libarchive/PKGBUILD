pkgname=ps5-payload-libarchive
pkgver=3.7.4
pkgrel=1
pkgdesc='library that can create and read several streaming archive format'
arch=('x86_64')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=("http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz")
sha256sums=('7875d49596286055b52439ed42f044bd8ad426aa4cc5aabd96bfe7abb971d5e8')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')
depends=('ps5-payload-xz')

build() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd libarchive-$pkgver

    export LDFLAGS="-lkernel_sys"
    ./configure --prefix=${PS5_SYSROOT} \
		--host=x86_64 \
		--disable-shared --enable-static  \
		--disable-bsdtar --disable-bsdcat \
		--disable-bsdcpio --disable-acl --without-xml2
    ${MAKE}
}

package() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd libarchive-$pkgver

    make install DESTDIR="$pkgdir"
}

