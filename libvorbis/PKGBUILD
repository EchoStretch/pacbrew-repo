pkgname=ps5-payload-libvorbis
pkgver=1.3.7
pkgrel=2
pkgdesc='Reference implementation of the Ogg Vorbis audio format'
arch=('any')
url='https://www.xiph.org/vorbis'
license=('BSD')
source=("https://ftp.osuosl.org/pub/xiph/releases/vorbis/libvorbis-$pkgver.tar.gz")
sha256sums=('0e982409a9c3fc82ee06e08205b1355e5c6aa4c36bca58146ef399621b0ce5ab')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libvorbis-$pkgver

    ${CMAKE} -DCMAKE_BUILD_TYPE=Release \
	     -B build \
             -S .
    ${MAKE} -C build
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libvorbis-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} -C build install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"
}
