pkgname=ps5-payload-a52dec
pkgver=0.8.0
pkgrel=1
pkgdesc='Library for decoding ATSC A/52 (AC-3) audio streams'
arch=('any')
url='https://git.adelielinux.org/community/a52dec/'
license=('GPL-2.0-or-later')
source=("https://git.adelielinux.org/community/a52dec/-/archive/v$pkgver/a52dec-v$pkgver.tar.gz")
sha256sums=('470d084074d15416a0f0f300b85b61ddcc1ce105ad14a2aac670a1767ecba8de')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd a52dec-v$pkgver
    autoreconf -fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd a52dec-v$pkgver

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--disable-oss
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd a52dec-v$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    make install
    install -Dm644 -t "${DESTDIR}/${PREFIX}/include/a52dec/" liba52/a52_internal.h

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"

}
