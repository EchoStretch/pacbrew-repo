pkgname=ps5-payload-libpng
pkgver=1.6.43
pkgrel=1
pkgdesc='PNG format graphic files library'
arch=('x86_64')
url='http://www.libpng.org'
license=('custom')
options=(!strip libtool staticlibs)
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")
sha256sums=('6a5ca0652392a2d7c9db2ae5b40210843c0bbc081cbd410825ab00cc59f14a6c')
makedepends=('ps5-payload-sdk')
depends=('ps5-payload-zlib')
groups=('ps5-payload-dev')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libpng-$pkgver

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--disable-tests
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd libpng-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"

    mkdir -p "${pkgdir}/${PS5_PAYLOAD_SDK}/bin"
    mv "${DESTDIR}/${PREFIX}/bin/libpng16-config" \
       "${pkgdir}/${PS5_PAYLOAD_SDK}/bin/prospero-libpng-config"
    rm "${DESTDIR}/${PREFIX}/bin/libpng-config"
}
