pkgname=ps5-payload-curl
pkgver=8.8.0
pkgrel=1
pkgdesc='An URL retrieval utility and library'
arch=('x86_64')
url='https://curl.haxx.se'
license=('curl')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.xz")
sha256sums=('0f58bb95fc330c8a46eeb3df5701b0d90c9d9bfcc42bd1cd08791d12551d4400')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
depends=('ps5-payload-zlib' 'ps5-payload-mbedtls')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd curl-$pkgver
    sed -i 's|define USE_XATTR| |g' src/tool_xattr.h
    autoreconf -fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd curl-$pkgver

    # Optimizing with default -O2 causes issues at src/tool_operate.c:1157
    # where outs->stream is set to NULL.
    export CFLAGS="-O1"

    ./configure --prefix="${PS5_HBROOT}" --host=x86_64 \
		--disable-shared --enable-static --with-mbedtls \
		--disable-docs
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd curl-$pkgver

    export DESTDIR="${pkgdir}/${PS5_SYSROOT}"
    make install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PS5_HBROOT}"

    mkdir -p "${pkgdir}/${PS5_PAYLOAD_SDK}/bin"
    mv "${DESTDIR}/${PS5_HBROOT}/bin/curl-config" \
       "${pkgdir}/${PS5_PAYLOAD_SDK}/bin/prospero-curl-config"
}
