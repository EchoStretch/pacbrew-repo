pkgname=ps5-payload-curl
pkgver=8.10.1
pkgrel=1
pkgdesc='An URL retrieval utility and library'
arch=('any')
url='https://curl.haxx.se'
license=('curl')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.xz")
sha256sums=('73a4b0e99596a09fa5924a4fb7e4b995a85fda0d18a2c02ab9cf134bebce04ee')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
depends=('ps5-payload-zlib' 'ps5-payload-wolfssl')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd curl-$pkgver
    sed -i 's|define USE_XATTR| |g' src/tool_xattr.h
    autoreconf -fi
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd curl-$pkgver

    # Fetch CA bundle
    ./scripts/mk-ca-bundle.pl

    # Optimizing with default -O2 causes issues at src/tool_operate.c:1157
    # where outs->stream is set to NULL.
    export CFLAGS="-O1"

    ./configure --prefix="${PREFIX}" --host=x86_64-pc-freebsd \
		--enable-static --disable-shared \
		--without-libpsl --with-wolfssl \
		--with-ca-bundle="${PREFIX}/etc/ca-bundle.crt" \
		--disable-docs
    ${MAKE}
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd curl-$pkgver

    export DESTDIR="${pkgdir}/${DESTDIR}"
    ${MAKE} install

    ${PS5_CROSS_FIX_ROOT} "${DESTDIR}/${PREFIX}"

    mkdir -p "${DESTDIR}/${PREFIX}/etc"
    cp ca-bundle.crt "${DESTDIR}/${PREFIX}/etc"

    mkdir -p "${pkgdir}/${PS5_PAYLOAD_SDK}/bin"
    ln -s "../target/${PREFIX}/bin/curl-config" \
       "${pkgdir}/${PS5_PAYLOAD_SDK}/bin/prospero-curl-config"
}
