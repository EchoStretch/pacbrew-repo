pkgname=ps5-payload-curl
pkgver=8.8.0
pkgrel=1
pkgdesc='An URL retrieval utility and library'
arch=('x86_64')
url='https://curl.haxx.se'
license=('curl')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.xz")
sha256sums=('0f58bb95fc330c8a46eeb3df5701b0d90c9d9bfcc42bd1cd08791d12551d4400')
options=(!strip libtool staticlibs)
groups=('ps5-payload-dev')
depends=('ps5-payload-zlib' 'ps5-payload-mbedtls')
makedepends=('ps5-payload-sdk')

prepare() {
    cd curl-$pkgver
    sed -i 's|define USE_XATTR| |g' src/tool_xattr.h
}

build() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd curl-$pkgver

    # Optimizing with default -O2 causes issues at src/tool_operate.c:1157
    # where outs->stream is set to NULL.
    export CFLAGS="-O1"

    autoreconf -fi
    ./configure --prefix=${PS5_SYSROOT} --host=x86_64 \
		--disable-shared --enable-static --with-mbedtls
    ${MAKE}
}

package() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd curl-$pkgver

    ${MAKE} install DESTDIR="$pkgdir"
}
