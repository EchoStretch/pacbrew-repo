pkgname=ps5-payload-mbedtls
pkgver=3.6.0
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('x86_64')
url='https://tls.mbed.org'
license=('Apache2')
options=(!strip libtool staticlibs)
source=("https://github.com/Mbed-TLS/mbedtls/releases/download/v${pkgver}/mbedtls-${pkgver}.tar.bz2")
sha256sums=('3ecf94fcfdaacafb757786a01b7538a61750ebd85c4b024f56ff8ba1490fcd38')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

prepare() {
    if [ -z "${PS5_PAYLOAD_SDK}" ]; then
	export PS5_PAYLOAD_SDK=/opt/ps5-payload-sdk
    fi

    cd mbedtls-$pkgver
    scripts/config.py unset MBEDTLS_SSL_PROTO_TLS1_3
    scripts/config.py set MBEDTLS_HAVE_SSE2
    scripts/config.py set MBEDTLS_THREADING_C
    scripts/config.py set MBEDTLS_THREADING_PTHREAD
}

build() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd mbedtls-$pkgver

    ${CMAKE} -DCMAKE_BUILD_TYPE=Release \
	     -DENABLE_TESTING=NO \
	     -DENABLE_PROGRAMS=NO \
	     -B build \
	     -S .
    ${MAKE} -C build
}

package() {
    source "${PS5_PAYLOAD_SDK}/toolchain/prospero.sh"
    cd mbedtls-$pkgver

    ${MAKE} -C build DESTDIR="${pkgdir}/${PS5_SYSROOT}" install
}
