pkgname=ps5-payload-mbedtls
pkgver=3.6.0
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('any')
url='https://tls.mbed.org'
license=('apache')
options=(!strip libtool staticlibs)
makedepends=('ps5-payload-sdk')
source=("https://github.com/Mbed-TLS/mbedtls/releases/download/v${pkgver}/mbedtls-${pkgver}.tar.bz2")
groups=('ps5-payload-dev')
sha256sums=('3ecf94fcfdaacafb757786a01b7538a61750ebd85c4b024f56ff8ba1490fcd38')

build() {
  source /opt/pacbrew/ps5/payload-sdk/shell/toolchain.sh

  cd mbedtls-$pkgver

  ${CMAKE} -B build \
    -DCMAKE_INSTALL_PREFIX=${PS5_SYSROOT}/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TESTING=0 \
    -DENABLE_PROGRAMS=0 \
   .
  make -C build
}

package() {
  source /opt/pacbrew/ps5/payload-sdk/shell/toolchain.sh

  cd mbedtls-$pkgver

  make -C build DESTDIR="$pkgdir" install
}
