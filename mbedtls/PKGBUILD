pkgname=ps5-payload-mbedtls
pkgver=3.6.0
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('x86_64')
url='https://tls.mbed.org'
license=('Apache2')
options=(!strip libtool staticlibs)
source=("https://github.com/Mbed-TLS/mbedtls/releases/download/v${pkgver}/mbedtls-${pkgver}.tar.bz2")
sha256sums=('3ecf94fcfdaacafb757786a01b7538a61750ebd85c4b024f56ff8ba1490fcd38')
groups=('ps5-payload-dev')
makedepends=('ps5-payload-sdk')

build() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd mbedtls-$pkgver

    ${CMAKE} -DCMAKE_INSTALL_PREFIX=${PS5_SYSROOT} \
	     -DCMAKE_BUILD_TYPE=Release \
	     -B build \
	     -S .
    ${MAKE} -C build
}

package() {
    source /opt/pacbrew/ps5/payload-sdk/toolchain/prospero.sh
    cd mbedtls-$pkgver

    ${MAKE} -C build install DESTDIR="$pkgdir"
}
